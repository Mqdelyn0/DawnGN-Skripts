# ----------[ AUTHOR INFORMATION ]---------- #
# Discord Tag: ¬‿¬ GekkeSchaap#3897
# Discord Server: https://discord.gg/bKwWyKM
# Minecraft User: GekkeSchaap
# Github: https://github.com/GekkeSchaap
# ----------[ AUTHOR INFORMATION ]---------- #
#
#
#
# ----------[ SKRIPT INFORMATION ]---------- #
# Server Version: Paper 1.16.4
# Skript Addons:
# Last Updated: 11/17/20
# Current Version: 0.1.0
# ----------[ SKRIPT INFORMATION ]---------- #



# ------------[ CONFIGURATION ]------------ #

options:

    # MESSAGES CONFIGURATION
    message_prefix: &2&lDawnGN &8¬&7
    message_borders: &8&m                                                &r

    # COLORS CONFIGURATION
    color_first: &7
    color_second: &a
    color_third: &2
    color_fourth: &8
    color_error: &c

    # ERRORS CONFIIGURATION
    # Use <arg> for the permission, correct usage, or incorrect type. <type> for the "error_not_correct_type"
    error_no_permission: &r%nl%&c&l(!) &cI can't let you do that!%nl%&c&l(!) &cMissing Permission: &a<arg>&r%nl%
    error_not_player: &r%nl%&c&l(!) &cOnly players can execute this command!&r%nl%
    error_wrong_usage: &r%nl%&c&l(!) &cIncorrect Usage!%nl%&c&l(!) &cCorrect Usage: &a<arg>&r%nl%
    error_not_correct_type: &r%nl%&c&l(!) &a<arg> &cisn't the required type! &8(&a<type>&8)&r%nl%

    # ALERTS CONFIGURATION
    # Use <script> for the skript file, And <reload_time> for the reloaded time.
    alerts_reload_enabled: true
    alerts_reload_message: &r%nl%&c&l(!) &a<script>.sk &7reloaded in &a<reload_time>!%nl%&r
    alerts_reloading_enabled: true
    alerts_reloading_message: &r%nl%&c&l(!) &a<script>.sk &7is being reloaded! This may cause lag.%nl%&r

    # GUI ITEMS CONFIGURATION
    # Here you can set the items used in GUIs.
    filler: gray stained glass pane named ""

    # LAG PROTECTION CONFIGURATION
    # These options are for the "algorithm" that should hopefully reduce lag caused by this Skript.
    low_lag_protection: 0.1 + ((20 - tps from the last minute) / 4) * 2 # These are used for code that generally isn't intensive, So you can leave it at pretty low numbers.
    medium_lag_protection: 1.5 + ((20 - tps from the last minute) / 2) * 2 # These are used for code that are kind of intensive, Like hologram reloading, Loops, And what not.
    high_lag_protection: 6 + (20 - tps from the last minute) * 2 # These are used for code that are what I think, really intensive, Like setting a lot of blocks, Or just in simple codes, YANDEREDEV CODE TYPE SHIT
# ------------[ CONFIGURATION ]------------ #



# --------------[ FUNCTIONS ]-------------- #

function addLevel(plr: player):
    set {_uuid} to uuid of {_plr}
    remove ({leveling::needed_exp::%{leveling::current_level::%{_uuid}%}%}+({leveling::current_prestige::%{_uuid}%}*0.75)) from {leveling::current_exp::%{_uuid}%}
    add 1 to {leveling::current_level::%{_uuid}%}
    send title "{@color_third}&lLEVEL UP!" with subtitle "{@color_first}Your new {@color_second}level {@color_first}is {@color_second}%{leveling::current_level::%{_uuid}%}%" to {_plr}
    play sound "entity.player.levelup" to {_plr}

# --------------[ FUNCTIONS ]-------------- #



# ---------------[ EVENTS ]--------------- #

# LOAD / UNLOAD MESSAGE
on load:
    if {@alerts_reload_enabled} = true:
        if {lib::unload_time} is not set:
            set {lib::unload_time} to now
        set {_message} to "{@alerts_reload_message}"
        replace "<script>" in {_message} with script
        replace "<reload_time>" in {_message} with "%difference between {lib::unload_time} and now%"
        Lib_Broadcast({_message})
    set {_loops} to 1
    loop 100 times:
        set {leveling::needed_exp::%loop-number - 1%} to (({leveling::needed_exp::%loop-number - 2%}  ? 10) + ({_loops} / 4))
        add 1 to {_loops}

on unload:
    set {lib::unload_time} to now
    if {@alerts_reloading_enabled} = true:
        set {_message} to "{@alerts_reloading_message}"
        replace "<script>" in {_message} with script
        Lib_Broadcast({_message})

on join:
    set {leveling::current_level::%player's uuid%} to 1 if {leveling::current_level::%player's uuid%} is not set
    set {leveling::current_exp::%player's uuid%} to 0 if {leveling::current_exp::%player's uuid%} is not set
    set {leveling::current_prestige::%player's uuid%} to 0 if {leveling::current_prestige::%player's uuid%} is not set

on join:
    while player is online:
        while {leveling::current_exp::%player's uuid%} >= ({leveling::needed_exp::%{leveling::current_level::%player's uuid%}%}+({leveling::current_prestige::%player's uuid%}*0.75)):
            addLevel(player)
            wait 5 ticks
        wait 5 ticks

# ---------------[ EVENTS ]--------------- #



# ---------------[ COMMANDS ]--------------- #

command /expadd [<integer>]:
    permission: *
    trigger:
        add arg-1 to {leveling::current_exp::%player's uuid%}

command /level [<player>]:
    trigger:
        if arg-1 is not set:
            set {_name} to player's uuid
            set {_name2} to player
        else:
            set {_name} to arg-1's uuid
            set {_name2} to arg-1
        send "{@color_third}&lLEVEL INFO:"
        send " "
        send "{@color_second} Name of player: %{_name2}%"
        send "{@color_second} Current Exp: {@color_first}%{leveling::current_exp::%{_name}%}%"
        send "{@color_second} Needed Exp: {@color_first}%({leveling::needed_exp::%{leveling::current_level::%{_name}%}%}+({leveling::current_prestige::%{_name}%}*0.75))%"
        send "{@color_second} Current Level: {@color_first}%{leveling::current_level::%{_name}%}%"
        send "{@color_second} Current Prestige: {@color_first}%{leveling::current_prestige::%{_name}%}%"
        send " "

command /prestige [<text>]:
    trigger:
        create a gui with virtual chest with 3 rows named "&9Prestige GUI":
            loop 54 times:
                make next gui slot with {@filler}
            make gui slot 11 with emerald named "{@color_third}&lCONFIRM PRESTIGE" with lore " ", "{@color_first}Click here to prestige." and " ":
                if {leveling::current_level::%player's uuid%} >= 100:
                    if Money_Get(player) >= 7500000+{leveling::current_prestige::%player's uuid%}*1100000:
                        Money_Remove(player, (7500000+{leveling::current_prestige::%player's uuid%}*1100000))
                        add 1 to {leveling::current_prestige::%player's uuid%}
                        set {leveling::current_exp::%player's uuid%} to 0
                        set {leveling::current_level::%player's uuid%} to 1
                        add 0.03 to {shop::multiplier::%uuid of player%}
                        play sound "block.note_block.pling" to all players
                        broadcast ""
                        broadcast " {@color_second}&kiii {@color_third}&lPRESTIGE! {@color_second}&n%player%{@color_second} prestiged to prestige &n%{leveling::current_prestige::%player's uuid%}%&r {@color_second}&kiii"
                        broadcast ""
                        close inventory of player
                    else:
                        close inventory of player
                        send "{@message_prefix} {@color_error}You don't have the required amount of money to prestige"
                        play sound "entity.villager.no" to player
                else:
                    close inventory of player
                    send "{@message_prefix} {@color_error}You need level &n100{@color_error} or above to prestige"
                    play sound "entity.villager.no" to player
            make gui slot 15 with paper named "{@color_third}&lPRESTIGE INFO" with lore " ", "{@color_first}You can start prestiging", "{@color_first}if you meet the following requirements:", "{@color_first} ● Level 100", "{@color_first} ● $%7500000+{leveling::current_prestige::%player's uuid%}*1100000%", " ", "{@color_first}You'll get the following in return:", "{@color_first} ● 0.03x Multiplier", "{@color_first} ● More soon." and " "
        open last gui to player

# ---------------[ COMMANDS ]--------------- #
